#!/bin/sh
# the next line restarts using tclsh -*- tcl -*- \
exec tclsh "$0" "$@"

package require tnm 3.0

##
## This functions is called whenever an SNMP trap is received.
##

proc traphandler {ip pdu args} {
    set msg "$pdu from $ip:"
    foreach vb $args {
	append msg " [tnm::mib name [lindex $vb 0]]=\"[lindex $vb 2]\""
    }
    puts stdout $msg
    tnm::syslog warning $msg
}

proc try_or_cry cmd {
    if {[catch $cmd msg]} { puts stderr "$cmd failed: $msg" }
}

try_or_cry { tnm::snmp listener -version SNMPv1 }
try_or_cry { tnm::snmp listener -version SNMPv2c }
try_or_cry { tnm::snmp listener -version SNMPv3 }

foreach s [tnm::snmp find] {
    $s bind trap "traphandler %A %T %V"
}

vwait forever
